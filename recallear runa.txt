;|--------------------------------------|
;| 'Viajar' con reactivos mediante runa |
;|--------------------------------------|

set #sysMsgCol 1391
event SysMessage 'Viajar' con reactivos mediante 'Runa' automatico -Activado-
set %RUNE QWL_SWL_RWL_PWL

gosub Control

Viajar:
{
                  ;TECLA [Ctrl] [Shift] [Alt]
  onHotKey G Ctrl ;TECLA: A-Z|0-9|F1-F12|ESC|BACK|TAB|ENTER|PAUSE|CAPSLOCK|SPACE|PGDN|PGUP|END|HOME|LEFT|RIGHT|UP|DOWN|PRNSCR|INSERT|DELETE|NUMLOCK|SCROLLLOCK
  {
    chooseSkill Mage Real
    if #skill > 399
    {
      findItem %RUNE C_ , #BACKPACKID
      if #findCnt > 0
      {
        event Macro 15 31
        if Necesitas_Raiz_de_Mandragora_para_este_hechizo. <> #sysMsg && Necesitas_Musgo_de_Sangre_para_este_hechizo. <> #sysMsg && Necesitas_Perla_Negra_para_este_hechizo. <> #sysMsg
        {
          set %lTargetID #lTargetID
          set #lTargetID #findID
          set #lTargetKind 1
          target
          event Macro 22 0
          set #lTargetID %lTargetID
        }
      }
      else
      {
        event ExMsg #charID 3 1970 [ , RUNA , ]
      }
      wait 3
    }
    else
    {
      event ExMsg #charID 3 1970 [ , MAGIA , ]
    }
  }
}
goto Viajar

sub Control
{
  findItem * C_ , #backpackID
  if #findCnt = 0
  {
    set %xBackpackID #cliXRes + #cliLeft
    set %yBackpackID #cliYRes + #cliTop
    nextCPos %xBackpackID %yBackpackID
    wait 10
    event SysMessage ~ * , Abriendo la 'Mochila' de , #SPC , #charName , ...
    event Macro 8 7
  }
  event Macro 15 31
  target 6
  if Necesitas_ in #sysMsg
  {
    sound
    set #sysMsgCol 1970
    event SysMessage ~[ATENCION]: No tienes los reactivos necesarios.
  }
  else
  {
    set #targCurs 0
  }
  findItem %RUNE C_ , #BACKPACKID
  if #findCnt = 0
  {
    sound
    set #sysMsgCol 1970
    event SysMessage ~[ATENCION]: No hay ninguna 'Runa' en el primer nivel de tu 'Mochila'.
  }
  if #findCnt > 1
  {
    sound
    set #sysMsgCol 1970
    event SysMessage ~[ATENCION]: Hay #findCnt runas en el primer nivel de tu 'Mochila'.
  }
  return
}
