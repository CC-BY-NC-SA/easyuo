;|-----------------------------------|
;| 'Viajar' con varita mediante runa |
;|-----------------------------------|

set #sysMsgCol 1391
event SysMessage 'Viajar' con 'Varita' mediante 'Runa' automatico -Activado-
set #sysMsgCol 1970
event SysMessage ~[ATENCION]: Solo tienes que disponer de una unica 'Varita' con la misma apariencia que la de 'Viajar' en el primer nivel de tu 'Mochila'. Si quieres llevar otras 'Varitas' con igual aspecto siempre podras hacerlo estando estas dentro de otro contenedor.
set %RUNE QWL_SWL_RWL_PWL
set %TYPEVARITA BFF

goSub Control

Viajar:
{
                  ;TECLA: A-Z|0-9|F1-F12|ESC|BACK|TAB|ENTER|PAUSE|CAPSLOCK|SPACE|PGDN|PGUP|END|HOME|LEFT|RIGHT|UP|DOWN|PRNSCR|INSERT|DELETE|NUMLOCK|SCROLLLOCK
  onHotKey G Ctrl ;TECLA [Ctrl] [Shift] [Alt]
  {
    findItem %RUNE C_ , #BACKPACKID
    if #findCnt > 0
    {
      set %lObjectID #lObjectID
      set #lObjectID %VARITA
      event Macro 17 0
      set #lObjectID %lObjectID
      set %lTargetID #lTargetID
      set #lTargetID #findID
      set #lTargetKind 1
      target
      event Macro 22 0
      set #lTargetID %lTargetID
    }
    else
    {
      event ExMsg #charID 3 1970 [ , SIN RUNA , ]
    }
    wait 3
  }
}
goTo Viajar

sub Control
{
  findItem #backpackID C_ , #charID
  if #findCnt = 0
  {
    set %xCharID #cliXRes + #cliLeft
    set %yCharID #cliTop
    nextCPos %xCharID %yCharID
    event SysMessage ~ * , Abriendo la ficha de , #SPC , #charName , ...
    event Macro 8 1
  }
  findItem * C_ , #backpackID
  if #findCnt = 0
  {
    set %xBackpackID #cliXRes + #cliLeft
    set %yBackpackID #cliYRes + #cliTop
    nextCPos %xBackpackID %yBackpackID
    wait 10
    event SysMessage ~ * , Abriendo la 'Mochila' de , #SPC , #charName , ...
    event Macro 8 7
  }
  wait 6
  findItem %TYPEVARITA C_ , #BACKPACKID
  if #findCnt = 0
  {
    sound
    set #sysMsgCol 1970
    event SysMessage ~[ATENCION]: No hay ninguna 'Varita' de 'Viajar' en el primer nivel de tu 'Mochila'.
  }
  else
  {
    set %VARITA #findID
  }
  findItem %RUNE C_ , #BACKPACKID
  if #findCnt = 0
  {
    sound
    set #sysMsgCol 1970
    event SysMessage ~[ATENCION]: No hay ninguna 'Runa' en el primer nivel de tu 'Mochila'.
  }
  if #findCnt > 1
  {
    sound
    set #sysMsgCol 1970
    event SysMessage ~[ATENCION]: Hay #findCnt runas en el primer nivel de tu 'Mochila'.
  }
  return
}
